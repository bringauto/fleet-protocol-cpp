CMAKE_MINIMUM_REQUIRED(VERSION 3.25 FATAL_ERROR)
PROJECT(fleet-protocol C CXX)

SET(CMAKE_POSITION_INDEPENDENT_CODE ON)
SET(CMAKE_CXX_STANDARD 20)

SET(FLEET_PROTOCOL_CXX_HELPERS_VERSION 0.1.1)
SET(FLEET_PROTOCOL_INTERFACE_VERSION 2.0.0)

FIND_PACKAGE(CMLIB
        COMPONENTS CMDEF CMUTIL
        REQUIRED
)

INCLUDE(GNUInstallDirs)

SET(MODULE_MAINTAINER_TARGET_NAME fleet-protocol-cxx-helpers)
SET(MODULE_MAINTAINER_ALIAS_NAME  ${PROJECT_NAME}::cxx-helpers)
SET(COMMON_HEADERS_TARGET         fleet-protocol-interface::common-headers-interface)

OPTION(BRINGAUTO_TESTS "Enable tests" OFF)
OPTION(BRINGAUTO_PACKAGE "Package creation" OFF)
OPTION(BRINGAUTO_INSTALL "Enable install" OFF)
OPTION(BRINGAUTO_SYSTEM_DEP "Enable system dependencies" OFF)
OPTION(BRINGAUTO_SAMPLES "Enable build of sample app, not used in project" OFF)

IF (BRINGAUTO_SYSTEM_DEP)
    FIND_PACKAGE(CMLIB
            COMPONENTS STORAGE
            REQUIRED
    )
    INCLUDE(cmake/Dependencies.cmake)
ENDIF ()

IF (BRINGAUTO_PACKAGE)
    IF (NOT BRINGAUTO_INSTALL)
        SET(BRINGAUTO_INSTALL ON CACHE BOOL "Forced install due to BRINGAUTO_PACKAGE=ON" FORCE)
        MESSAGE(WARNING "BRINGAUTO_INSTALL is switched to on because of BRINGAUTO_PACKAGE=ON")
    ENDIF ()
ENDIF ()

FIND_PACKAGE(fleet-protocol-interface ${FLEET_PROTOCOL_INTERFACE_VERSION} REQUIRED)

FILE(GLOB_RECURSE source_files ${CMAKE_CURRENT_LIST_DIR}/source/*)

CMDEF_ADD_LIBRARY(
        LIBRARY_GROUP ${MODULE_MAINTAINER_TARGET_NAME}
        TYPE STATIC
        SOURCES ${source_files}
        INCLUDE_DIRECTORIES "${CMAKE_CURRENT_LIST_DIR}/include/"
        INSTALL_INCLUDE_DIRECTORIES "include/"
        VERSION ${FLEET_PROTOCOL_CXX_HELPERS_VERSION}
)

TARGET_LINK_LIBRARIES(${MODULE_MAINTAINER_TARGET_NAME}-static PUBLIC
        ${COMMON_HEADERS_TARGET}
)

ADD_LIBRARY(${MODULE_MAINTAINER_ALIAS_NAME} ALIAS "${MODULE_MAINTAINER_TARGET_NAME}-static")

IF (BRINGAUTO_INSTALL)
    CMDEF_INSTALL(
            TARGET ${MODULE_MAINTAINER_TARGET_NAME}-static
            NAMESPACE ${MODULE_MAINTAINER_TARGET_NAME}-static::
    )
ENDIF ()

IF (BRINGAUTO_PACKAGE)
    CMDEF_PACKAGE(
            MAIN_TARGET ${MODULE_MAINTAINER_TARGET_NAME}-static
            VERSION ${FLEET_PROTOCOL_CXX_HELPERS_VERSION}
    )
    SET(CPACK_GENERATOR ZIP)
    SET(CPACK_PACKAGE_CONTACT "BringAuto s.r.o. <maintainers@bringauto.com>")
    INCLUDE(CPack)
ENDIF ()

IF (BRINGAUTO_TESTS)
    ENABLE_TESTING()
    ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/test/)
ENDIF (BRINGAUTO_TESTS)
